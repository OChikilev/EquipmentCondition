Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОборудованиеОрганизации
	Движения.ОборудованиеОрганизации.Очистить();
	Движения.ОборудованиеОрганизации.Записать();
	Движения.ОборудованиеОрганизации.Записывать = Истина;

	СостояниеОборудования = Перечисления.СостояниеОборудования.НаСкладе;
	СостоянмеВЭксплуатации = Перечисления.СостояниеОборудования.ВЭксплуатации;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВводВЭксплуатациюПереченьОборудования.Оборудование КАК Оборудование,
	|	СУММА(ВводВЭксплуатациюПереченьОборудования.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_Док
	|ИЗ
	|	Документ.ВводВЭксплуатацию.ПереченьОборудования КАК ВводВЭксплуатациюПереченьОборудования
	|ГДЕ
	|	ВводВЭксплуатациюПереченьОборудования.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВводВЭксплуатациюПереченьОборудования.Оборудование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОборудованиеОрганизацииОстатки.СостояниеОборудования КАК СостояниеОборудования,
	|	ОборудованиеОрганизацииОстатки.ГоденДо КАК ГоденДо,
	|	ОборудованиеОрганизацииОстатки.КоличествоОстаток КАК КоличествоОстаток,
	|	ОборудованиеОрганизацииОстатки.СуммаОстаток КАК СуммаОстаток,
	|	ОборудованиеОрганизацииОстатки.Оборудование,
	|	ОборудованиеОрганизацииОстатки.Оборудование.СрокЭксплуатации
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	РегистрНакопления.ОборудованиеОрганизации.Остатки(&Дата, Оборудование В
	|		(ВЫБРАТЬ
	|			ВТ_Док.Оборудование КАК Оборудование
	|		ИЗ
	|			ВТ_Док КАК ВТ_Док)
	|	И СостояниеОборудования = &СостояниеОборудования) КАК ОборудованиеОрганизацииОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Док.Оборудование КАК Оборудование,
	|	ВТ_Док.Количество КАК Количество,
	|	ВТ_Док.Оборудование.СрокЭксплуатации,
	|	ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) КАК КоличествоОстаток,
	|	ЕСТЬNULL(ВТ_Остатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ЕСТЬNULL(ВТ_Остатки.ГоденДо, ДАТАВРЕМЯ(1, 1, 1)) КАК ГоденДо,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_Остатки.КоличествоОстаток, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ВТ_Остатки.СуммаОстаток, 0) / ВТ_Остатки.КоличествоОстаток
	|	КОНЕЦ КАК СебестоимостьЕдиницы
	|ИЗ
	|	ВТ_Док КАК ВТ_Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Остатки КАК ВТ_Остатки
	|		ПО ВТ_Док.Оборудование = ВТ_Остатки.Оборудование
	|
	|УПОРЯДОЧИТЬ ПО
	|	ГоденДо
	|ИТОГИ
	|	СУММА(КоличествоОстаток),
	|	СУММА(Количество)
	|ПО
	|	Оборудование";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("СостояниеОборудования", СостояниеОборудования);
	Запрос.УстановитьПараметр("Дата", Дата);

	РезультатЗапроса = Запрос.Выполнить();

	ВыборкаОборудование = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

	Пока ВыборкаОборудование.Следующий() Цикл

		Если ВыборкаОборудование.Количество > ВыборкаОборудование.КоличествоОстаток Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.ИдентификаторНазначения = СтрШаблон("Недостаточно оборудования %1. Остаток: %2",
				ВыборкаОборудование.Оборудование.Наименование, ВыборкаОборудование.КоличествоОстаток);
			Сообщение.Сообщить();
			Отказ = Истина;
			Продолжить;
		КонецЕсли;

		ВыборкаДетальныеЗаписи = ВыборкаОборудование.Выбрать();
		ОсталосьСписать = ВыборкаОборудование.Количество;

		Пока ВыборкаДетальныеЗаписи.Следующий() И ОсталосьСписать > 0 Цикл

			Списываем = Мин(ОсталосьСписать, ВыборкаДетальныеЗаписи.КоличествоОстаток);
			СуммаСписания = ВыборкаДетальныеЗаписи.СуммаОстаток;
			Если Списываем <> ВыборкаДетальныеЗаписи.КоличествоОстаток Тогда
				СуммаСписания = Списываем * ВыборкаДетальныеЗаписи.СебестоимостьЕдиницы;
			КонецЕсли;

			Движение = Движения.ОборудованиеОрганизации.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Оборудование = ВыборкаДетальныеЗаписи.Оборудование;
			Движение.СостояниеОборудования = СостояниеОборудования;
			Движение.ГоденДо = ВыборкаДетальныеЗаписи.ГоденДо;
			Движение.Количество = Списываем;
			Движение.Сумма = СуммаСписания;
			
			Движение = Движения.ОборудованиеОрганизации.Добавить();
			Движение.Период = Дата;
			Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
			Движение.Оборудование = ВыборкаДетальныеЗаписи.Оборудование;
			Движение.СостояниеОборудования = СостоянмеВЭксплуатации;
			Движение.ГоденДо = ВыборкаДетальныеЗаписи.ГоденДо;
			Движение.ЭксплуатироватьДо = ДобавитьМесяц(Дата, ВыборкаДетальныеЗаписи.ОборудованиеСрокЭксплуатации);
			Движение.Количество = Списываем;
			Движение.Сумма = СуммаСписания;
		
		ОсталосьСписать = ОсталосьСписать - Списываем;
			
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры