#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Дата = НачалоМесяца(Дата);

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыбытиеОборудовантя.Ссылка,
	|	ВыбытиеОборудовантя.Представление
	|ИЗ
	|	Документ.ВыбытиеОборудовантя КАК ВыбытиеОборудовантя
	|ГДЕ
	|	ВыбытиеОборудовантя.Дата = &Дата
	|	И ВыбытиеОборудовантя.Ссылка <> &Ссылка
	|	И ВыбытиеОборудовантя.Проведен";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Дата", Дата);

	РезультатЗапроса = Запрос.Выполнить();

	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();

	ВыборкаДетальныеЗаписи.Следующий();

	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = СтрШаблон("На указанный месяц уже введен документ: %1", ВыборкаДетальныеЗаписи.Представление);
	Сообщение.Сообщить();
	Отказ = Истина;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)
	
	// регистр ОборудованиеОрганизации
	Движения.ОборудованиеОрганизации.Очистить();
	Движения.ОборудованиеОрганизации.Записывать = Истина;
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОборудованиеОрганизацииОстатки.Оборудование,
		|	ОборудованиеОрганизацииОстатки.СостояниеОборудования,
		|	ОборудованиеОрганизацииОстатки.ГоденДо,
		|	ОборудованиеОрганизацииОстатки.ЭксплуатироватьДо,
		|	ОборудованиеОрганизацииОстатки.КоличествоОстаток,
		|	ОборудованиеОрганизацииОстатки.СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ОборудованиеОрганизации.Остатки(&Дата, ГоденДо < &Дата
		|	ИЛИ ЭксплуатироватьДо МЕЖДУ ДАТАВРЕМЯ(1, 1, 1) И &Дата) КАК ОборудованиеОрганизацииОстатки";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Движение = Движения.ОборудованиеОрганизации.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, ВыборкаДетальныеЗаписи);
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
	КонецЦикла;


КонецПроцедуры
#КонецОбласти
#КонецЕсли